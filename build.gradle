buildscript {
    ext {
        grailsVersion = "7.0.0-M2"
    }
}

plugins {
    id "com.bertramlabs.asset-pipeline" version "5.0.7"
    id "groovy"
    id "java-library"
    id "maven-publish"
    id "org.grails.grails-plugin" version "7.0.0-M2"
    id "signing"
}

group = "io.github.zyro23"
version = "2.7.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://repo.grails.org/grails/core"
    }
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.grails:grails-plugin-controllers"
    api "org.springframework:spring-messaging"
    api "org.springframework:spring-websocket"
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar.archiveClassifier = ""

assets.packagePlugin = true

publishing {
    repositories {
        maven {
            name = "mavenBuild"
            url = layout.buildDirectory.dir("maven")
        }
        maven {
            name = "mavenCentralSnapshots"
            url = "https://central.sonatype.com/repository/maven-snapshots"
            credentials(PasswordCredentials)
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
            }
            pom {
                name = "io.github.zyro23:grails-spring-websocket"
                description = "Spring WebSocket Plugin"
                url = "https://github.com/zyro23/grails-spring-websocket"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "zyro"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/zyro23/grails-spring-websocket.git"
                    developerConnection = "scm:git:ssh://github.com:zyro23/grails-spring-websocket.git"
                    url = "https://github.com/zyro23/grails-spring-websocket"
                }
            }
        }
    }
}

signing {
    sign(publishing.publications.maven)
}
