buildscript {
    ext {
        grailsVersion = "6.2.0"
    }
}

plugins {
    id "groovy"
    id "java-library"
    id "org.grails.grails-plugin" version "6.2.0"
    id "com.bertramlabs.asset-pipeline" version "4.4.0"
    id "maven-publish"
    id "signing"
}

group = "io.github.zyro23"
version = "2.6.0-RC1"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind"
    api "org.grails:grails-plugin-controllers"
    api "org.springframework:spring-messaging"
    api "org.springframework:spring-websocket"
}

jar.archiveClassifier = ""

assets.packagePlugin = true

publishing {
    repositories {
        maven {
            name("mavenBuild")
            url(layout.buildDirectory.dir("maven"))
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
            }
            pom {
                name = "io.github.zyro23:grails-spring-websocket"
                description = "Spring WebSocket Plugin"
                url = "https://github.com/zyro23/grails-spring-websocket"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        name = "zyro"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/zyro23/grails-spring-websocket.git"
                    developerConnection = "scm:git:ssh://github.com:zyro23/grails-spring-websocket.git"
                    url = "https://github.com/zyro23/grails-spring-websocket"
                }
            }
        }
    }
}

signing {
    sign(publishing.publications.maven)
}